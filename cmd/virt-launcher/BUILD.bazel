load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@bazeldnf//:deps.bzl", "xattrs")

go_library(
    name = "go_default_library",
    srcs = ["virt-launcher.go"],
    importpath = "kubevirt.io/kubevirt/cmd/virt-launcher",
    visibility = ["//visibility:private"],
    deps = [
        "//pkg/cloud-init:go_default_library",
        "//pkg/config:go_default_library",
        "//pkg/container-disk:go_default_library",
        "//pkg/ephemeral-disk:go_default_library",
        "//pkg/hooks:go_default_library",
        "//pkg/hotplug-disk:go_default_library",
        "//pkg/ignition:go_default_library",
        "//pkg/util:go_default_library",
        "//pkg/virt-handler/cmd-client:go_default_library",
        "//pkg/virt-launcher:go_default_library",
        "//pkg/virt-launcher/notify-client:go_default_library",
        "//pkg/virt-launcher/virtwrap:go_default_library",
        "//pkg/virt-launcher/virtwrap/agent-poller:go_default_library",
        "//pkg/virt-launcher/virtwrap/api:go_default_library",
        "//pkg/virt-launcher/virtwrap/cli:go_default_library",
        "//pkg/virt-launcher/virtwrap/cmd-server:go_default_library",
        "//pkg/virt-launcher/virtwrap/util:go_default_library",
        "//staging/src/kubevirt.io/api/core/v1:go_default_library",
        "//staging/src/kubevirt.io/client-go/log:go_default_library",
        "//vendor/github.com/spf13/pflag:go_default_library",
        "//vendor/k8s.io/apimachinery/pkg/types:go_default_library",
        "//vendor/k8s.io/apimachinery/pkg/util/wait:go_default_library",
        "//vendor/k8s.io/apimachinery/pkg/watch:go_default_library",
        "//vendor/libvirt.org/go/libvirt:go_default_library",
    ],
)

load("//vendor/kubevirt.io/client-go/version:def.bzl", "version_x_defs")

go_binary(
    name = "virt-launcher",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
    x_defs = version_x_defs(),
)

load(
    "@io_bazel_rules_docker//container:container.bzl",
    "container_image",
)
load("@io_bazel_rules_docker//contrib:group.bzl", "group_entry", "group_file")
load("@io_bazel_rules_docker//contrib:passwd.bzl", "passwd_entry", "passwd_file")
load("@bazel_tools//tools/build_defs/pkg:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "virt-launcher-tar",
    srcs = [
        "node-labeller/node-labeller.sh",
        ":virt-launcher",
        "//cmd/container-disk-v2alpha:container-disk",
        "//cmd/virt-freezer",
        "//cmd/virt-launcher-monitor",
        "//cmd/virt-probe",
    ],
    package_dir = "/usr/bin",
)

xattrs(
    name = "setcaps",
    capabilities = {
        "/usr/bin/virt-launcher": [
            "cap_net_bind_service",
            "cap_sys_ptrace",
        ],
    },
    selinux_labels = {
        "/usr/bin/virt-launcher": "system_u:object_r:container_file_t:s0",
    },
    tar = ":virt-launcher-tar",
)

passwd_entry(
    name = "root-user",
    gid = 0,
    home = "/root",
    info = "root",
    shell = "/bin/bash",
    uid = 0,
    username = "root",
)

group_entry(
    name = "qemu-group",
    gid = 107,
    groupname = "qemu",
)

group_entry(
    name = "root-group",
    gid = 0,
    groupname = "root",
)

group_file(
    name = "group",
    entries = [
        ":qemu-group",
        ":root-group",
    ],
)

passwd_entry(
    name = "qemu-user",
    gid = 107,
    home = "/home/qemu",
    shell = "/bin/bash",
    uid = 107,
    username = "qemu",
)

passwd_file(
    name = "passwd",
    entries = [
        ":qemu-user",
        ":root-user",
    ],
)

pkg_tar(
    name = "libvirt-config",
    srcs = [
        ":libvirtd.conf",
        ":qemu.conf",
    ],
    package_dir = "/etc/libvirt",
)

pkg_tar(
    name = "passwd-tar",
    srcs = [
        ":group",
        ":passwd",
    ],
    mode = "0644",
    package_dir = "etc",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "nsswitch-tar",
    srcs = [
        ":nsswitch.conf",
    ],
    mode = "0644",
    package_dir = "/etc",
)

container_image(
    name = "version-container",
    directory = "/",
    files = ["//:get-version"],
    tars = select({
        "@io_bazel_rules_go//go/platform:linux_arm64": [
            ":libvirt-config",
            ":passwd-tar",
            ":nsswitch-tar",
            "//rpm:launcherbase_aarch64",
        ],
        "//conditions:default": [
            ":libvirt-config",
            ":passwd-tar",
            ":nsswitch-tar",
            "//rpm:launcherbase_x86_64",
        ],
    }),
)

container_image(
    name = "virt-launcher-image",
    architecture = select({
        "@io_bazel_rules_go//go/platform:linux_arm64": "arm64",
        "//conditions:default": "amd64",
    }),
    base = ":version-container",
    entrypoint = ["/usr/bin/virt-launcher"],
    tars = [":setcaps"],
    visibility = ["//visibility:public"],
)
